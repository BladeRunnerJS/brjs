// from http://forums.gradle.org/gradle/topics/creating_sbprojects_dynamically_without_having_a_settings_gradle

import static groovy.io.FileVisitResult.*  
import static groovy.io.FileType.*

def skipDirs = ~/^(build|\..*|src|out)/  
def preDir = {  
	if (skipDirs.matcher(it.name).matches())  
		return SKIP_SUBTREE  
}

def getProjectName(String dir) {
	def normalisedDirPath = dir.replace("\\", "/")
	def normalisedRootDirPath = rootDir.toString().replace("\\", "/")
	def projectName = normalisedDirPath.minus(normalisedRootDirPath + "/").replaceAll("/", ":")
	return projectName  
}

rootDir.traverse(  
	type: DIRECTORIES,  
	preDir: preDir) { dir->  
	def dstr = dir.toString()  
	if ( (!skipDirs.matcher(dir.name)) && new File(dstr + "/build.gradle").exists() ) {  
		include getProjectName(dstr)  
  }  
}